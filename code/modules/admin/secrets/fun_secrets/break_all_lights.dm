/datum/admin_secret_item/fun_secret/break_all_lights
	name = "Break Lights"

/datum/admin_secret_item/fun_secret/break_all_lights/execute(mob/user)
	. = ..()
	if (!.)
		return
	var/choice = input("Break Lights in:") as null | anything in list("Current Area", "Current Level", "Connected Levels", "All Lights")
	if (!choice)
		return
	switch (choice)
		if ("Current Area")
			var/area/usr_area = get_area(user)
			if (!usr_area)
				return to_chat(user, SPAN_DANGER("Invalid area!"))
			for (var/obj/machinery/power/apc/apc in usr_area)
				apc.overload_lighting()
		if ("Current Level")
			var/user_z = get_z(user)
			if (!user_z)
				return to_chat(user, SPAN_DANGER("Invalid Z-Level!"))
			for (var/obj/machinery/power/apc/apc in SSmachines.machinery)
				if (apc.z == user_z)
					apc.overload_lighting()
		if ("Connected Levels")
			var/list/user_levels = get_z(user)
			if (!user_levels)
				return to_chat(user, SPAN_DANGER("Invalid Z-Level!"))
			user_levels = GetConnectedZlevels(user_levels)
			for (var/obj/machinery/power/apc/apc in SSmachines.machinery)
				if (apc.z in user_levels)
					apc.overload_lighting()
		if ("All Lights")
			lightsout(0, 0)
